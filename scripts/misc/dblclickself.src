use uo;
use os;
use npc;

include "include/eventID";
include ":itemUtils:itemdesc";

program dblclickself( who )
	var mount := GetEquipmentByLayer( who, 25 );
	if (!mount)
		OpenPaperdoll( who, who );
		return;
	endif
	if (who.warmode)
		OpenPaperdoll( who, who );
		return;
	endif
	// This allows a script to run prior to unequipping
	// a mount for any efects or mods to the player.
	var unequip := GetObjProperty(mount, "Unequip");
	if(unequip)
		Run_Script(unequip, {who});
	endif
    var serial := GetObjProperty(mount, "serial");
	var animal := SystemFindObjectBySerial(CInt(serial));
    EraseObjProperty(animal,"serial");
    EraseObjProperty(animal,"mounted");
    EraseObjProperty(animal,"mounted_on");
	animal.facing := who.facing;
	var ev := array;
    ev.+ type;
    ev.+ source;
    ev.type := EVID_WAKEUP;
    ev.source := who;
    SendEvent(animal, ev);
    MoveObjectToLocation(animal, who.x, who.y, who.z, who.realm, MOVEOBJECT_FORCELOCATION);
    RestartScript(animal);
	MoveItemToContainer(mount, who.backpack);
	DestroyItem( mount );
endprogram
use uo;
use os;
use npc;

include "include/eventID";
include ":itemUtils:itemdesc";

program dblclickself( who )
	var mount := GetEquipmentByLayer( who, 25 );
//	var cfg_elem := GetItemDescInfo(item.objtype);
	if (!mount)
		OpenPaperdoll( who, who );
		return;
	endif
	if (who.warmode)
		OpenPaperdoll( who, who );
		return;
	endif

	var un_equip_script := mount.UnEquipScript;
	if(un_equip_script)
		if(GetObjProperty(mount, "RewardMount"))
			Run_Script_To_Completion(un_equip_script, who);
			Sleepms(800);
			DestroyItem(mount);
			return 1;
		endif
	endif

    var serial := GetObjProperty(mount, "serial");
	var animal := SystemFindObjectBySerial(CInt(serial));
    EraseObjProperty(animal,"serial");
    EraseObjProperty(animal,"mounted");
    EraseObjProperty(animal,"mounted_on");
	animal.facing := who.facing;
	var ev := array;
    ev.+ type;
    ev.+ source;
    ev.type := EVID_WAKEUP;
    ev.source := who;
    SendEvent(animal, ev);
    MoveObjectToLocation(animal, who.x, who.y, who.z, who.realm, MOVEOBJECT_FORCELOCATION);
    RestartScript(animal);
	DestroyItem( mount );
endprogram